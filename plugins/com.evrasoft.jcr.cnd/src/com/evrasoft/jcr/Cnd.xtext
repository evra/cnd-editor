grammar com.evrasoft.jcr.Cnd hidden(WS, ML_COMMENT, SL_COMMENT) 

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate cnd "http://www.evrasoft.com/jcr/Cnd"

Model :
	 (namespaces+=NsMapping | nodeTypes+=NodeTypeDefinition)*;

NodeTypeDefinition:
	'[' name=JcrString ']' 
	
	('>' declaredSupertypeNames+=[NodeTypeDefinition|JcrString] (',' declaredSupertypeNames+=[NodeTypeDefinition|JcrString] /*|'?' //variant  */)* )?  
		
	(
		orderable?=Orderable? &
		mixin?=Mixin? &
		abstract?=Abstract? & 
		queryable?=Queryable? &
		notqueryable?=Notqueryable? & //TODO find a way to map query flags on one field
		
		(('primaryitem'| '!')(primaryItem=[ItemDefinition|JcrString] /* | '?' //variant */ ))?
		
	)
	 
	(	
	 	declaredPropertyDefinitions+=PropertyDefinition |
		declaredChildNodeDefinitions+=NodeDefinition
	)*  
;

/**
 * Creates ItemDefinition dummy model element as parent 
 * of NodeDefinition and PropertyDefinition. It corresponds 
 * to JCR ItemDefinition abstract class. 
 */
ItemDefinition :
	NodeDefinition | PropertyDefinition

	//Properties shared by NodeDefinition and PropertyDefinition
	//	(name=JcrString|name='*' /*Jackrabbit feature */) //ItemDef
	//	(
	//		autocreated?=Autocreated? & //ItemDef
	//		manadatory?=Mandatory? &	//ItemDef
	//		protected?=Protected? &		//ItemDef
	//		onParentVersion=Opv?       //ItemDef
	//	)
;

NodeDefinition:// returns ItemDefinition:
	
	{NodeDefinition}
	
	'+' (name=JcrString|name='*' /*Jackrabbit feature */) //ItemDef
	
	('(' (requiredTypes+=[NodeTypeDefinition|JcrString] (',' requiredTypes+=[NodeTypeDefinition|JcrString])* /*| '?' //variant  */) ')')?
	 	
	('=' (defaultType=[NodeTypeDefinition|JcrString] /*| '?' //variant  */))?
	 
	(
		autocreated?=Autocreated? & //ItemDef
		manadatory?=Mandatory? &	//ItemDef
		protected?=Protected? &		//ItemDef
		onParentVersion=Opv? &      //ItemDef
		
		sameNameSiblings?=Sns? &
		primary?=Primary? /* Jackrabbit feature */
	)
;

Sns:
	('sns' | '*' | 'multiple' /* Jackrabbit feature*/) /* '?' //variant */
;

PropertyDefinition:// returns ItemDefinition:
	
	{PropertyDefinition}
	
	'-' (name=JcrString|name='*' /*Jackrabbit feature */) //ItemDef 

	('(' type=PropertyType')')? 

	('=' (defaultValues+=JcrString (',' defaultValues+=JcrString)* /* | '?' //variant */))?
	 
	(
		autocreated?=Autocreated? & //ItemDef
		manadatory?=Mandatory? &	//ItemDef
		protected?=Protected? &		//ItemDef
		onParentVersion=Opv? &      //ItemDef
		
		multiple?=Multiple? & 
		queryOps=QueryOps? & 					
		noFullText?=NoFullText? & 
		noQueryOrder?=NoQueryOrder? &
		primary?=Primary? /* Jackrabbit feature */
	)

	('<' (valueConstraints+=JcrString (',' valueConstraints+=JcrString)* /* | '?' //variant */))?
;



NoQueryOrder:
	('noqueryorder' | 'nqord')/* '?' //variant */
;

NoFullText:
	('nofulltext' | 'nof') /* '?' //variant */
;

QueryOps:
	('queryops' | 'qop') ( JcrString /*| '?' //variant */)
;


//TODO does not work. probably conflict with STRING terminal: 

//QueryOps:
//	(('queryops' | 'qop') "'" queryOperators+=Operator ("," queryOperators+=Operator)* "'" /* | '?' //variant  */)? &
//;
//Operator:
//	'=' | '<>' | '<' | '<=' | '>' | '>=' | 'LIKE'
//;

Multiple:
	('multiple' | 'mul' | '*') /* '?' //variant */
;

enum Opv:
	COPY='COPY' |
	VERSION='VERSION' |
	INITIALIZE='INITIALIZE' |
	COMPUTE='COMPUTE' |
	IGNORE='IGNORE' |
	ABORT='ABORT' 
	//| ('OPV' '?') //variant
;

Protected:
	('protected' | 'pro' | 'p') /* '?' //variant */
;

Mandatory:
	('mandatory' | 'man' | 'm') /* '?' //variant */
;

Autocreated:
	('autocreated' | 'aut' | 'a' )/* '?' //variant */
;

Primary:	
	//This attribute is not defined by JSR-283
	('primary'|'!') /* Jackrabbit feature. */	
;


/*
 *  The order of the enum elements is important. 
 *  It defines same ordinal as in javax.jcr.PropertyType
 *  Do not change the order!  
 */
enum PropertyType:
	 UNDEFINED='UNDEFINED'|
	 TSTRING='STRING' | 
	 TBINARY='BINARY' |
	 TLONG='LONG' |
	 TDOUBLE='DOUBLE' |
	 DDATE='DATE' |
	 TBOOLEAN='BOOLEAN' |	
	 TNAME='NAME' |
	 TPATH='PATH' |
	 TREFERENCE='REFERENCE' | 
	 TWEAKREFERENCE='WEAKREFERENCE' |
	 TURI='URI' |
	 TDECIMAL='DECIMAL' 
	 	 
	 //| '?' /* variant */	  
;


Queryable:
	('query' | 'q' )
;
Notqueryable:
	('noquery' | 'nq')
;

Abstract:
	('abstract' | 'abs' | 'a') /* '?' //variant */
;

Mixin:
	('mixin' | 'mix' | 'm') /* '?' //variant */
;

Orderable:
	('orderable' | 'ord' | 'o') /* '?' //variant */	
;

NsMapping :
	 '<' name=JcrString '=' uri=Uri '>';


Uri: 
	 JcrString;

JcrString:
	STRING | UNQUOTED_STRING | ('mix') | ('name')		
;

terminal UNQUOTED_STRING:
	 //dots in names are not allowed by the JSR but some implementations support them 
	 ('a'..'z'|'A'..'Z'|'_'|'0'..'9'|':'|'*'|'.')*
;

//TODO 
//According to JCR2.0 string terminal must follow XML char definition  /* see ยง3.2.2 Local Names */

//see NCNAME in Antlr XPath grammar http://www.antlr3.org/grammar/1264460091565/XPath2.g

//terminal UNQUOTED_STRING : (XML_CHAR)+;
//terminal fragment XML_CHAR : ('\u0022'..'\u007E');


terminal STRING	: 
			'"' ( '\\' ('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') | !('\\'|'"') )* '"' |
			"'" ( '\\' ('b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\') | !('\\'|"'") )* "'"
		; 
terminal ML_COMMENT	: '/*' -> '*/';
terminal SL_COMMENT 	: '//' !('\n'|'\r')* ('\r'? '\n')?;

terminal WS			: (' '|'\t'|'\r'|'\n')+;

